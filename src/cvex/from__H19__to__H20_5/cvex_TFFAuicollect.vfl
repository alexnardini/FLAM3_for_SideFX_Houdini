/*  
 /  Tested on:  Houdini 19.x
 /              Houdini 19.5
 /              Houdini 20.x
 /              Houdini 20.5
 /
 /  Title:      FLAM3H. SideFX Houdini FLAM3: 2D
 /  Author:     Alessandro Nardini
 /  date:       October 2020, Last revised February 2024
 /  License:    GPL
 /  Copyright:  2021, Â© F stands for liFe ( made in Italy )
 /
 /  info:       Based on the original: "The Fractal Flame Algorithm"
 /  Authors:    Scott Draves, Erik Reckase
 /
 /  Paper:      https://flam3.com/flame_draves.pdf
 /  Date:       September 2003, Last revised November 2008
 /
 /  Github:     https://github.com/scottdraves/flam3
 /  Date:       December 2002, Last revised May 2015
 /
 /  Name:       TFFAUICOLLECT "CVEX"
 /
 /  Comment:    UI FLAM3H viewport TAG infos collection.
*/


#pragma opname  "TFFAuicollect"
#pragma oplabel "TFFAuicollect"
#pragma opmininputs 1
#pragma opmaxinputs 3


// TAG GENOME
struct gemtag{

    // ITERATORS
    string sIDX[];
    int    RES, p1t[], p2t[], v1t[], v2t[], v3t[], v4t[], P1t[];
    float  pbw[], p1w[], p2w[], v1w[], v2w[], v3w[], v4w[], P1w[];
    
    void gemtagBuild(){

        // SYS
        sIDX = detail(2, "sIDX");
        RES = detail(2, "RES");
        // WEIGHTS
        pbw = detail(2, "pbw");
        p1w = detail(2, "p1w");
        p2w = detail(2, "p2w");
        v1w = detail(2, "v1w");
        v2w = detail(2, "v2w");
        v3w = detail(2, "v3w");
        v4w = detail(2, "v4w");
        P1w = detail(2, "P1w");
        // TYPES
        p1t = detail(2, "p1t");
        p2t = detail(2, "p2t");
        v1t = detail(2, "v1t");
        v2t = detail(2, "v2t");
        v3t = detail(2, "v3t");
        v4t = detail(2, "v4t");
        P1t = detail(2, "P1t");
    }
}


// TAG GENOME FF
struct gemtagff{

    // FF
    int    fp1t, fv1t, fv2t, fP1t, fP2t;
    float  fp1w, fv1w, fv2w, fP1w, fP2w; 

    void gemtagffBuild(const int FF){

        if(FF){
            // WEIGHTS
            fp1w = detail(3, "fp1w");
            fv1w = detail(3, "fv1w");
            fv2w = detail(3, "fv2w");
            fP1w = detail(3, "fP1w");
            fP2w = detail(3, "fP2w");
            // TYPES
            fp1t = detail(3, "fp1t");
            fv1t = detail(3, "fv1t");
            fv2t = detail(3, "fv2t");
            fP1t = detail(3, "fP1t");
            fP2t = detail(3, "fP2t");
        }
    }

}


void
_TFFAuicollect(string _bound_VARTYPE[]){

    string T[];
    string Ts[] = { "linear", 
                    "sinusoidal",
                    "spherical",
                    "swirl",
                    "horseshoe",
                    "polar",
                    "handkerchief",
                    "heart",
                    "disc",
                    "spiral",
                    "hiperbolic",
                    "diamond",
                    "ex",
                    "julia",
                    "bent",
                    "waves",
                    "fisheye",
                    "popcorn",
                    "exponential",
                    "power",
                    "cosine",
                    "rings",
                    "fan",
                    "bubble",
                    "cylinder",
                    "eyefish",
                    "blur",
                    "curl",
                    "ngon",
                    "pdj",
                    "blob",
                    "juliaN",
                    "juliaScope",
                    "gaussian_blur",
                    "fan2",
                    "rings2",
                    "rectangles",
                    "radialblur",
                    "pie",
                    "arch",
                    "tangent",
                    "square",
                    "rays",
                    "blade",
                    "secant2",
                    "twintrian",
                    "cross",
                    "disc2",
                    "supershape",
                    "flower",
                    "conic",
                    "parabola",
                    "bent2",
                    "bipolar",
                    "boarders",
                    "butterfly",
                    "cell",
                    "cpow",
                    "edisc",
                    "elliptic",
                    "noise",
                    "escher",
                    "foci",
                    "lazysusan",
                    "loonie",
                    "pre blur",
                    "modulus",
                    "oscope",
                    "polar2",
                    "popcorn2",
                    "scry",
                    "separation",
                    "split",
                    "splits",
                    "stripes",
                    "wedge",
                    "wedge julia",
                    "wedge sph",
                    "whorl",
                    "waves2",
                    "exp",
                    "log",
                    "sin",
                    "cos",
                    "tan",
                    "sec",
                    "csc",
                    "cot",
                    "sinh",
                    "cosh",
                    "tanh",
                    "sech",
                    "csch",
                    "coth",
                    "auger",
                    "flux",
                    "mobius",
                    "curve",
                    "perspective",
                    "bwraps",
                    "hemisphere",
                    "polynomial",
                    "crop",
                    "unpolar",
                    "glynnia",
                    "point_symmetry" };


    // SYS
    int FF, RIP, MB, F3C, iter, inum, xaos_chk, xaos_mode;
    FF   = chi("doff");
    RIP  = chi("rip");
    MB   = chi("domb");
    // From Fractorium: flam3 comptibility (f3c). Check inside variations.h to see both versions of each.
    // The behavior of the:
    // cos, cosh, cot, coth, csc, csch, sec, sech, sin, sinh, tan and tanh variations
    // are different in flam3/Apophysis versus Chaotica.
    //      Checked:    use the Apophysis behavior.
    //      Unchecked:  use the Chaotica behavior.
    F3C = chi("f3c");
    iter = chi("iter");
    inum = chi("flamefunc");
    xaos_chk = detail(1, "XS");
    xaos_mode = chi("xm");

    // GEM TAG
    gemtag GEMTAG;
    GEMTAG->gemtagBuild();
    string sIDX[]; /* CAST */ sIDX=GEMTAG.sIDX;
    int    RES, p1t[], p2t[], v1t[], v2t[], v3t[], v4t[], P1t[];   /* CAST */ RES=GEMTAG.RES; p1t=GEMTAG.p1t; p2t=GEMTAG.p2t; v1t=GEMTAG.v1t; v2t=GEMTAG.v2t; v3t=GEMTAG.v3t; v4t=GEMTAG.v4t; P1t=GEMTAG.P1t;
    float  pbw[], p1w[], p2w[], v1w[], v2w[], v3w[], v4w[], P1w[]; /* CAST */ pbw=GEMTAG.pbw; p1w=GEMTAG.p1w; p2w=GEMTAG.p2w; v1w=GEMTAG.v1w; v2w=GEMTAG.v2w; v3w=GEMTAG.v3w; v4w=GEMTAG.v4w; P1w=GEMTAG.P1w;
    // GEM TAG tmp
    float ppbw, ppw1, ppw2, w1, w2, w3, w4, Pw1;
    // GEM FF TAG
    gemtagff GEMTAGFF;
    GEMTAGFF->gemtagffBuild(FF);
    int    fp1t, fv1t, fv2t, fP1t, fP2t; /* CAST */ fp1t=GEMTAGFF.fp1t; fv1t=GEMTAGFF.fv1t; fv2t=GEMTAGFF.fv2t; fP1t=GEMTAGFF.fP1t; fP2t=GEMTAGFF.fP2t;
    float  fp1w, fv1w, fv2w, fP1w, fP2w; /* CAST */ fp1w=GEMTAGFF.fp1w; fv1w=GEMTAGFF.fv1w; fv2w=GEMTAGFF.fv2w; fP1w=GEMTAGFF.fP1w; fP2w=GEMTAGFF.fP2w;

    // BUILD TAG
    append(T, concat("[", itoa(iter), "]"));
    if(xaos_chk){
        if(!xaos_mode) append(T, concat("[", "Xt", "]"));   // xaos TO
        else append(T, concat("[", "fX", "]"));             // xaos FROM
    }

    if(RIP) append(T, "RIP");
    if(F3C) append(T, "F3C");
    if(MB) append(T, "MB");

    for(int i=0; i<RES; ++i){

        string ITERATOR_TAG, ITERNUM, W, PRE[], PB, V1, V2, V3, V4, POST[];
        ITERNUM = concat("(", sIDX[i], ")");

        // VAR 1
        w1 = v1w[i];
        if(w1!=0) V1 = Ts[v1t[i]]+":";
        else V1 = ":";
        // VAR 2
        w2 = v2w[i];
        if(w2!=0) V2 = Ts[v2t[i]]+":";
        else V2 = ":";
        // VAR 3
        w3 = v3w[i];
        if(w3!=0) V3 = Ts[v3t[i]]+":";
        else V3 = ":";
        // VAR 4
        w4 = v4w[i];
        if(w4!=0) V4 = Ts[v4t[i]]+":";
        else V4 = ":";
        // VAR weights total
        float w_total = w1+w2+w3+w4;
        
        // PRE BLUR
        ppbw = pbw[i];
        if(ppbw>0){ if(w_total != 0) PB="*"; }
        // PRE VAR 1
        ppw1 = p1w[i];
        if(ppw1>0){ if(w_total != 0) append(PRE, Ts[p1t[i]]); }
        // PRE VAR 2
        ppw2 = p2w[i];
        if(ppw2>0){ if(w_total != 0) append(PRE, Ts[p2t[i]]); }
        // Build pre vars tag string
        string pre_build;
        if(len(PRE)==1) pre_build = concat( "[ ", PRE[0], " ]:");
        else if(len(PRE)>1) pre_build = concat( "[ ", join(PRE, ", "), " ]:");
        else pre_build = "";
        // POST VAR 1
        Pw1 = P1w[i];
        if(Pw1>0) { if(w_total != 0) append(POST, Ts[P1t[i]]); }
        // Build post vars tag string
        string post_build;
        if(len(POST)==1) post_build = concat( "[ ", POST[0], " ]:");
        else if(len(POST)>1) post_build = concat( "[ ", join(POST, ", "), " ]:");
        else post_build = "";
        // Iterator weight
        if(w_total != 0) W = sprintf( "%g", chf(concat("iw_", sIDX[i]))); else W = "";
        
        // Reformat iterator weights string floats
        string out[] = {};
        string SPLIT[] = split(W, ".");
        if(len(SPLIT)>1){
            int count = 1;
            foreach(string s; SPLIT){ append(out, s[:(6/count)]); count++; }
        }
        else out = SPLIT;
        W = join(out, ".");
        ITERATOR_TAG = concat(PB, ITERNUM, pre_build, V1, V2, V3, V4, post_build, W);

        append(T, ITERATOR_TAG);
        PRE = {};
    }

    if(FF){

        string FF_TAG, FV1, FV2, FPRE[], FPOST[];

        // FF VAR 1
        if(fv1w!=0) FV1 = Ts[fv1t]+":";
        else FV1 = ":";
        // FF VAR 1
        if(fv2w!=0) FV2 = Ts[fv2t]+":";
        else FV2 = ":";
        float fw_total = fv1w+fv2w;
        // FF PRE 1
        if(fp1w>0){ if(fw_total != 0) append(FPRE, Ts[fp1t]); }
        string FPRE_build;
        if(len(FPRE)==1) FPRE_build = concat( "[ ", FPRE[0], " ]:");
        else if(len(FPRE)>1) FPRE_build = concat( "[", join(FPRE, ", "), "]:");
        else FPRE_build = "";
        // FF POST 1
        if(fP1w>0){ if(fw_total != 0) append(FPOST, Ts[fP1t]); }
        // FF POST 2
        if(fP2w>0){ if(fw_total != 0) append(FPOST, Ts[fP2t]); }
        // Build post vars tag string
        string FPOST_build;
        if(len(FPOST)==1) FPOST_build = concat( "[ ", FPOST[0], " ]");
        else if(len(FPOST)>1) FPOST_build = concat( "[ ", join(FPOST, ", "), " ]");
        else FPOST_build = "";

        FF_TAG = concat(FPRE_build, FV1, FV2, FPOST_build);
        append(T, toupper(FF_TAG));
        }
        
    _bound_VARTYPE = T;
}

cvex
TFFAuicollect( export string VARTYPE[] = {} )
{
    _TFFAuicollect(VARTYPE);
}
