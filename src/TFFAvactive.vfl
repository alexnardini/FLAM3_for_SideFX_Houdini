/*  
 /  Title:      SideFX Houdini FRACTAL FLAME generator: 2D
 /  Author:     Alessandro Nardini
 /  date:       October 2020, Last revised January 2022
 /
 /  info:       Based on the original: "The Fractal Flame Algorithm"
 /  Authors:    Scott Draves, Erik Reckase
 /  Weblink:    https://flam3.com/flame_draves.pdf
 /  Date:       September 2003, Last revised November 2008
 /
 /  Name:       TheFractalFlameAlgorithm    "VEX"
 /
 /  Comment:    Collect variation ACTIVE and XAOS.
 /
*/


#ifndef VOP_OP
#define VOP_OP
#endif
#ifndef VOP_CVEX
#define VOP_CVEX
#endif

#pragma opname  "TFFAvactive"
#pragma oplabel "TFFAvactive"
#pragma opmininputs 1
#pragma opmaxinputs 1

#define EPS 1e-10

void XAOS_transpose(const float XAOS[]; float T[]; const int size){
    for(int i=0; i<size; i++){
        for(int j=0; j<size; j++){
            int idx = j*size+i;
            append(T, XAOS[idx]);
        }
    }
}

void
_TFFAvactive(int _bound_VACTIVE[]; float _bound_XAOS_TO[], _bound_XAOS_FROM[])
{
    int iter_f = chi("../flamefunc");
    string sIDX[];
    int vactive[];
    float IW[];
    // ACTIVE vars
    //
    for(int i=0; i<iter_f; i++){
        string iIDX = itoa(i+1);
        append(vactive, chi(concat("../vactive_", iIDX)));
        float IWcheck = chf(concat("../iw_", iIDX));
        if(IWcheck==0) vactive[i]=0;
        if(!vactive[i]) continue;
        append(IW, chf(concat("../iw_", iIDX)));
        append(sIDX, iIDX);
    }

    // XAOS
    //
    int size = len(sIDX);
    float _XAOS_TO[], XAOS_TO[], _XAOS_FROM[], XAOS_FROM[];
    for(int i=0; i<size; i++){

        string varnote = chs(concat("../varnote_", sIDX[i]));
        string note[] = split(varnote, ":");
        float xaos[];

        if(toupper(note[0])=="XAOS"){
            pop(note, 0);
            foreach(string n; note){
                append(xaos, max(atof(n), EPS));
            }
            resize(xaos, iter_f, 1);
        }
        else resize(xaos, iter_f, 1);
        
        for(int j=0; j<iter_f;j++){
            if(!vactive[j]) pop(xaos, j);
        }
        
        float IW_xaos[];
        for(int k=0; k<size; k++){
            append(IW_xaos, IW[i]*IW[k]*xaos[k]);
        }

        append(_XAOS_TO, IW_xaos);
        append(XAOS_TO, create_cdf(IW_xaos));
    }
    // Just in case I need it...do I ? not sure yet
    XAOS_transpose(_XAOS_TO, _XAOS_FROM, size);
    for(int i=0; i<size; i++){
        int sl = i*size;
        append(XAOS_FROM, create_cdf(_XAOS_FROM[sl:sl+size]));
    }

    // setdetailattrib(0, "__FROM", _XAOS_FROM, "set");
    // setdetailattrib(0, "__TO", _XAOS_TO, "set");
    _bound_VACTIVE = vactive;
    _bound_XAOS_TO = XAOS_TO;
    _bound_XAOS_FROM = XAOS_FROM;
}

cvex
TFFAvactive(export int VACTIVE[] = {};
            export float XAOS_TO[] = {};
            export float XAOS_FROM[] = {};)
{
    _TFFAvactive(VACTIVE, XAOS_TO, XAOS_FROM);
}
